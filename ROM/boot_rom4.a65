
setup_basic4_xl	.(

	; where do we store the extra patch code
IEEE_PATCH		=$fe20		; IEEE fix for faster CPU clocks
SDC_COMPANION	=$fe30		; SD-Card link
USB_COMPANION	=$df00		; USB link

	; -----------------------
	; copy over @MON 
	lda #$10
	ldx #$a000
	ldy #$1000
	jsr flash2direct_xl
	
	; -----------------------
	; copy over PET BASIC4 $b000-$dfff
	lda #$c0
	ldx #$b000
	ldy #$3000
	jsr flash2direct_xl

	; copy over PET KERNEL4 $f000-$ffff
	lda #$f0
	ldx #$f000
	ldy #$1000
	jsr flash2direct_xl

	pla		; bits 7/6/5 have number of editor ROM
			; bit 7: 1=orig, 0=patched
			; bit 6: 1=80 col, 0=40 col
			; bit 5: 1=c64, 0=PET-N kbd
			; 000 = PET-N, 40 col, patched
			; 001 = C64,   40 col, patched
			; 010 = PET-N, 80 col, patched
			; 011 = C64,   80 col, patched
			; 100 = PET-N, 40 col, orig
			; 101 = C64,   40 col, orig
			; 110 = PET-N, 80 col, orig
			; 111 = C64,   80 col, orig
	pha
	and #%11100000
	lsr
	ldx #$e000
	ldy #$1000
	jsr flash2direct2_xl

	; check USB patch
	pla
	pha
	and #%00001000
	beq nousb

	; copy over petromcomp to destination at $dfxx
	lda #$0f		; page in flash
	ldx #USB_COMPANION	; target in bank 0
	ldy #$0100		; length
	jsr flash2direct_xl

        ; set X/Y registers to 8 bit ...
        sep #%00010000
        .xs
	
	; install it into the ROM
	jsr $df03

	rep #%00010000
	.xl

	; reverse write protect done there
	lda #0
	sta $e801
nousb	
	; check BASIC4 patch
	pla
	pha
	bmi b4_orig
	jsr patch_rom4_xl
b4_orig
	; check 80 column bit
	pla
	pha
	and #%01000000
	beq col40
	lda #%00000010
	sta VCTRL
col40
	; check 8296 bit
	pla
	and #%00010000
	beq not_96
	; 8296
	lda #%11101000
	.byt $2c
not_96	lda #%11100000

	jmp end


patch_rom4_xl .(

	; -------------------------------
	; SD Card patch

	; copy over SD-Card code
	lda #$c0	; page $c0 on 2nd 64k flash bank
	ldx #$4000	; to $4000
	ldy #$4000	; copy $4000 bytes
	jsr flash2direct2_xl
	
	ldx #$4000
l	lda $4000-1,x
	sta $074000-1,x
	dex
	bne l

  	; set X/Y registers to 8 bit ...
        sep #%00010000
        .xs

	jsr sdcard_install

	jsr opt_install

	rep #%00010000
	.xl

	; -------------------------------
	; IEEE speed patch

	; patch target area start
	ldx #IEEE_PATCH

	; from http://www.6502.org/users/andre/cbmhw/ryofat40/index.html
	lda #$4c
	cmp $f0d8	; is already patched with JMP (e.g. SDCard)
	bne nopatch

	ldy $f0d9	; XR/YR is 16 bit, copy over current JMP target to
	sty jmptarget	; patch JMP target

nopatch	sta $f0d8	; JMP
	stx $f0d9	; 16 bit address

	ldy #0
pl0	lda PATCH4_0,y
	sta 0,x
	inx
	iny
	cpy #12
	bne pl0

	rts

	; this patch code is in itself all relative,
	; so not address calculations are needed
PATCH4_0 .(
	lda #4
l1	sec
	sbc #1
	bne l1
	lda $e840
&jmptarget = *+1
	jmp $f0db
	.)

	.)		; end patch_rom4_xl

	.xs

#include "dosromcomp.a65"

#define	ROM
#include "patch4.a65"
#undef ROM
	.xl
	.)		; end setup_basic4_xl


